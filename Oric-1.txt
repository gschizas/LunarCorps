100 REM BASIC MONTH 4: LUNAR CORPS
110 REM HTTP://REDDIT.COM/R/RETROBATTLESTATIONS
120 REM WRITTEN BY FOZZTEXX

200 REM CLEAR SCREEN AND SETUP VARIABLES
210 CLS:X=1:Y=27:GOSUB 1010
220 PEAKS$ = " /\-":REM ORIC HAS NO UNDERSCORE
230 COLS = 37:ROWS = 27

300 GOSUB 2510
310 YS = SQR(2) * SQR(ROWS - TL + ZL) - 1
320 XE = INT((ZE - ZS) / 2) + ZS
330 XV = XE / YS:YV = 0
340 LX = 1:LY = 1

400 GOSUB 2010:LM$ = SP$
410 GOSUB 2010:ML$ = SP$:LE$ = SE$
420 FOR I = 1 TO INT(YS)
430 SP$ = LE$:X = LX:Y = LY:GOSUB 1510
440 YV = YV + 1
450 LX = LX + XV:LY = LY + YV
460 SP$ = LM$:IF I >= YS-2 THEN SP$ = ML$
470 X = LX:Y = LY:GOSUB 1510
480 FOR J = 1 TO 400:NEXT J
490 NEXT I
500 SP$ = LE$:X = LX:Y = LY:GOSUB 1510
510 LX = (ZE - ZS) / 2 + ZS - 1:LY = ROWS - TL + ZL
520 SP$ = LM$:X = LX:Y = LY:GOSUB 1510
530 SP$ = "THE EAGLE HAS LANDED":X = (COLS - LEN(SP$)) / 2:Y = ROWS / 2:GOSUB 1510

999 END

1000 REM POSITION CURSOR AT X,Y
1010 PRINT CHR$(30);
1020 IF Y > 1 THEN FOR I = 2 TO Y:PRINT:NEXT I
1030 IF X > 1 THEN FOR I = 2 TO X:PRINT CHR$(9);:NEXT I
1040 RETURN

1500 REM DRAW SPRITE IN SP$ AT X,Y
1510 SE = 1:SB = 1
1520 IF X < 1 OR X > COLS OR Y < 1 OR Y > ROWS THEN RETURN
1530 SE = SB
1540 SE = SE + 1:IF SE <= LEN(SP$) AND MID$(SP$,SE,1) <> CHR$(13) THEN 1540
1550 GOSUB 1010
1560 SL = SE - SB:IF SL + X - 1 > COLS THEN SL = COLS - X + 1
1570 PRINT MID$(SP$, SB, SL);
1580 IF SE >= LEN(SP$) THEN RETURN
1590 SB = SE:Y = Y + 1:GOTO 1520

2000 REM LOAD A SPRITE FROM DATA INTO SP$
2010 SP$ = "":SE$ = "":SR = 0:SC = 0
2020 READ T$,S$:IF LEN(SP$) > 0 THEN SP$ = SP$ + CHR$(13)
2030 SP$ = SP$ + S$:SR = SR + 1:IF LEN(S$) > SC THEN SC = LEN(S$)
2040 BL$ = "":FOR I = 1 TO LEN(S$):BL$ = BL$ + " ":NEXT I
2050 IF LEN(SE$) > 0 THEN SE$ = SE$ + CHR$(13)
2060 SE$ = SE$ + BL$
2070 IF T$ <> "SE" THEN GOTO 2020
2080 RETURN

2500 REM DRAW LUNAR SURFACE
2510 DIDUP = 0:L1$ = "":FOR I = 1 TO COLS + 4:L1$ = L1$ + " ":NEXT I
2520 ZS = 0:ZE = 0:TL = 0
2530 L2$ = "  ":UP = DIDUP: IF NOT UP AND MID$(L1$, 3, 1) <> " " THEN UP = 1
2540 IF NOT DIDUP AND UP THEN DIDUP = UP
2550 Z1 = 0:Z2 = 0
2560 FOR I = 3 TO COLS + 2
2570 C1$ = MID$(L2$, I-2, 1):C2$ = MID$(L2$, I-1, 1)
2580 C3$ = MID$(L1$, I-1, 1):C4$ = MID$(L1$, I, 1)
2590 C5$ = MID$(L1$, I+1, 1):C6$ = MID$(L1$, I+2, 1)
2600 IF PEAK = 0 AND MID$(L1$, I-1, 4) = "    " THEN PEAK = INT(RND(1) * 3):PEAK = PEAK + (PEAK = 2) * -1:PEAK = PEAK * INT(RND(1) * 2)
2610 IF  C2$ = "/" AND C4$ = " " THEN PEAK = 2
2620 IF (C1$ = "-" OR C1$ = "\") AND C2$ = " " THEN PEAK = 0
2630 IF C4$ <> " " THEN PEAK = 0
2640 IF (C3$ = "\" OR C3$ = "-") AND C4$ = " " THEN PEAK = 2
2650 IF C2$ = "-" AND C6$ = "/" THEN PEAK = 3
2660 IF (C2$ = "\" OR C2$ = " " OR C2$ = "-") AND C4$ = " " AND (C5$ = "-" OR C5$ = "/") THEN PEAK = 1
2670 IF C2$ = "\" AND C6$ <> " " THEN PEAK = 3
2680 IF (C5$ = "/" OR C5$ = "-") THEN PEAK = 1
2690 IF C4$ <> " " THEN PEAK = 0
2700 IF UP AND PEAK <> 2 THEN PEAK = 0
2710 IF PEAK = 1 THEN UP = 1
2720 IF PEAK = 2 THEN UP = 0
2730 P$ = MID$(PEAKS$, PEAK + 1, 1):PRINT P$;:L2$ = L2$ + P$
2740 IF PEAK = 3 AND Z1 = 0 THEN Z1 = I
2750 IF PEAK <> 3 AND C2$ = "-" THEN Z2 = I
2760 IF Z1 AND Z2 THEN Z3 = Z1:Z4 = Z2:Z1 = 0:Z2 = 0:IF Z4 - Z3 >= 3 AND Z3 > ZS THEN ZS = Z3:ZE = Z4:ZL = TL
2770 IF PEAK <> 3 OR (C1$ = "-" AND C2$ = "-") THEN PEAK = 0
2780 NEXT
2790 L1$ = L2$
2800 C2 = 0
2810 FOR I = 3 TO COLS+2
2820 IF MID$(L1$, I, 1) = " " THEN C2 = C2 + 1
2830 NEXT
2840 TL = TL + 1
2850 IF C2 < COLS THEN PRINT:GOTO 2530
2860 ZS = ZS - 2:ZE = ZE - 2:RETURN

9000 DATA SP,"(I)"
9010 DATA SE,"/T\"
9020 DATA SP,"(I)"
9030 DATA SP,"/T\"
9040 DATA SE," V "
